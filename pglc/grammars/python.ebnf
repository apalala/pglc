@@grammar :: Python
@@left_recursion :: False
@@parseinfo :: True
@@whitespace :: /(?:(?!\r?\n|\r)(?:\s|\\\n))+/
@@eol_comments :: /(?ms)(?:(?!\n)\s)*#[^\n]*?(?=\n|$)/
@@nameguard :: True
@@namechars :: '_'

@@keyword :: False      await      else       import     pass
@@keyword :: None       break      except     in         raise
@@keyword :: True       class      finally    is         return
@@keyword :: and        continue   for        lambda     try
@@keyword :: as         def        from       nonlocal   while
@@keyword :: assert     del        global     not        with
@@keyword :: async      elif       if         or         yield


start
    =
    @:(
      file_input
    | eval_input
    | single_input
    ) $
    ;


single_input
    =
    @:compound_stmt NEWLINE  | @:simple_stmt | NEWLINE
    ;


file_input
    =
    encoding:[ENCODING]
    stmt:{_ stmt _}* _ ENDMARKER
    ;


eval_input
    =
    @:testlist _ ENDMARKER
    ;


decorator
    =
    '@' name:dotted_name ['(' ~ args:[arglist] _ ')'] NEWLINE
    ;


decorators
    =
    {decorator}+
    ;


decorated
    =
    decorators:decorators def:(classdef | funcdef | async_funcdef)
    ;


async_funcdef
    =
    'async' @:funcdef
    ;


funcdef
    =
    'def' ~ name:name args:parameters
    ['->' type:test] ':' ~ [type:TYPE_COMMENT ~] body:func_body_suite
    ;


parameters
    =
    '(' _ @:[typedargslist] _ ')'
    ;


# NOTE:
# This version is from the reference documentation, adding TYPE_COMMENT
#       https://docs.python.org/3.8/reference/compound_stmts.html#function-definitions
typedargslist = parameter_list ;


parameter_list
    =
      parameter_list_posonly ~ [',' [parameter_list_no_posonly]]
    | parameter_list_no_posonly
    ;


parameter_list_posonly
    =
    defparameter {',' [typec] defparameter}* ',' [typec] '/'
    ;

parameter_list_no_posonly
    =
      defparameter {',' [typec] defparameter }* [',' [typec] [parameter_list_starargs]]
    | parameter_list_starargs
    ;

parameter_list_starargs
    =
    | '**' ~ parameter [',' ~ [typec]]
    | '*' ~ [parameter] {',' [typec] defparameter}*
      [',' [typec] ['**' ~ parameter [',' ~ [typec]]]]
    ;

defparameter            = param:parameter  ['=' ~ value:expression] ;
parameter               = name:identifier [':' ~ type:expression] ;
identifier              = name;
expression              = test;
typec                   = TYPE_COMMENT ~ ;


# FIXME: these are the rules bootstrapped from Grammar/Grammar
__original_typedargslist
    =
        '**' ~ tfpdef [','] [TYPE_COMMENT]
    |
        '*' !'*' ~ [tfpdef] {',' [TYPE_COMMENT] _ tfpdef ~ ['=' ~ test]}*
        (
                TYPE_COMMENT
            |
                [',' [TYPE_COMMENT] [ _ '**' tfpdef [','] [TYPE_COMMENT]]]
        )
    |
        tfpdef ['=' test] {',' _ [TYPE_COMMENT] tfpdef ~['=' test]}*
        (
                TYPE_COMMENT
            |
                [
                    ',' [TYPE_COMMENT]
                    [
                        |  '**' ~ tfpdef [','] [TYPE_COMMENT]
                        |  '*' !'*' ~ [tfpdef] {',' _ [TYPE_COMMENT] tfpdef ~ ['=' ~ test]}*
                            (
                                    TYPE_COMMENT
                                |
                                    [',' [TYPE_COMMENT] [_ '**' tfpdef [','] [TYPE_COMMENT]]]
                            )
                    ]
                ]
        )
    ;


tfpdef
    =
    name:name [ ':' ~ type:test]
    ;


vfpdef
    =
    name
    ;


varargslist
    =
      @:posarglist ~ [',' @:[varargslist_no_posonly]]
    | @:varargslist_no_posonly
    ;

posarglist
    =
    @:vararg {',' @:vararg}* ',' '/'
    ;

varargslist_no_posonly
    =
      @:vararg {',' @:vararg }* [',' @:[varargslist_starargs]]
    | @:varargslist_starargs
    ;

varargslist_starargs
    =
    | '**' ~ vararg [',' ~ ]
    | '*' ~ [vararg] {',' vararg}*
      [',' ['**' ~ vararg [',' ~ ]]]
    ;


vararg = name:name  ['=' ~ value:expression] ;


__original_varargslist
    =
    |   vfpdef ['=' _ test]
        {',' vfpdef ['=' _ test]}*
        [
             ','
            [
                  '*' [vfpdef] {',' vfpdef ['=' _ test]}* [',' ['**' vfpdef [',']]]
                | '**' vfpdef [',']
            ]
        ]
    | '*' [vfpdef] {',' vfpdef ['=' _ test]}* [',' ['**' vfpdef [',']]]
    | '**' vfpdef [',']
    ;


stmt
    =
    compound_stmt | simple_stmt
    ;


simple_stmt
    =
    @+:small_stmt {';' _ @+:small_stmt ~}* [';' ~] NEWLINE
    ;


small_stmt
    =
    | expr_stmt
    | flow_stmt
    | del_stmt
    | pass_stmt
    | global_stmt
    | nonlocal_stmt
    | assert_stmt
    | import_stmt
    ;


string_stmt
    =
    string
    ;


expr_stmt
    =
    exp+:testlist_star_expr
    (
          op:annassign
        | op:augassign value:(yield_expr | testlist)
        # FIXME: need to figure this out
        | {'=' exp+:(yield_expr | testlist_star_expr)}+ [TYPE_COMMENT]
        | ()
    )
    ;


annassign
    =
    ':' ~ type:test ['=' ~ value:(yield_expr | testlist)]
    ;


testlist_star_expr
    =
    @:(test | star_expr) {',' _ @:(test | star_expr)}* [',' ~]
    ;


augassign
    =
    | '+='
    | '-='
    | '*='
    | '@='
    | '/='
    | '%='
    | '&='
    | '|='
    | '^='
    | '<<='
    | '>>='
    | '**='
    | '//='
    ;


del_stmt
    =
    'del' ~ @:exprlist
    ;


pass_stmt
    =
    'pass' ~
    ;


flow_stmt
    =
    break_stmt | continue_stmt | return_stmt | raise_stmt | yield_stmt
    ;


break_stmt
    =
    'break' ~
    ;


continue_stmt
    =
    'continue' ~
    ;


return_stmt
    =
    'return' ~ @:[testlist_star_expr]
    ;


yield_stmt
    =
    yield_expr ~
    ;


raise_stmt
    =
    'raise' ~ [test:test ['from' ~ from:test]]
    ;


import_stmt
    =
    import_name | import_from
    ;


import_name
    =
    'import' ~ @:dotted_as_names
    ;


import_from
    =
    'from' ~ (up:{'.' | '...'}* parent:dotted_name | up:{'.' | '...'}+)
    'import' ~
    (
          '*' ~
        | '(' _ ~ modules:import_as_names _ ')'
        | modules:import_as_names
    )
    ;


import_as_name
    =
    name:name ['as' ~ as:name]
    ;


dotted_as_name
    =
    name:dotted_name ['as' ~ as:name]
    ;


import_as_names
    =
    @+:import_as_name {',' _ @+:import_as_name}* [',' ~]
    ;


dotted_as_names
    =
    @+:dotted_as_name {',' @+:dotted_as_name}*
    ;


dotted_name
    =
    @+:name {_ '.' _ ~ @+:name}*
    ;


global_stmt
    =
    'global' ~ @+:name {',' @+:name}*
    ;


nonlocal_stmt
    =
    'nonlocal' ~ @+:name {',' ~ @+:name}*
    ;


assert_stmt
    =
    'assert' @+:test [',' @+:test]
    ;


compound_stmt
    =
    | if_stmt
    | for_stmt
    | while_stmt
    | try_stmt
    | with_stmt
    | funcdef
    | classdef
    | decorated
    | async_stmt
    ;


async_stmt
    =
    'async' ~ @:(funcdef | with_stmt | for_stmt)
    ;


if_stmt
    =
    'if' ~ condition:namedexpr_test ':' ~ body:suite
    {EQDENT 'elif' ~ elif+:namedexpr_test ':' ~ suite}*
    [EQDENT 'else' ~ ':' ~ else:suite]
    ;


while_stmt
    =
    'while' ~ condition:namedexpr_test ':' ~ suite
    [EQDENT 'else' ~ ':' ~ else:suite]
    ;


for_stmt
    =
    'for' ~ exp:exprlist 'in' ~ target:testlist ':' ~ [TYPE_COMMENT ~] body:suite
    [EQDENT 'else' ~ ':' ~ else:suite]
    ;


try_stmt
    =
    'try' ~ ':' ~ body:suite
    (
            {EQDENT except_clause ~ ':' ~ except:suite}+ ~
            [EQDENT 'else' ~ ':' ~ else:suite]
            [EQDENT 'finally' ~ ':' ~ finally:suite]
        |
            EQDENT 'finally' ~ ':' ~ finally:suite
    )
    ;


with_stmt
    =
    'with' ~ item+:with_item {',' item+:with_item}* ':' ~
    type:[TYPE_COMMENT] body:suite
    ;


with_item
    =
    item:test ['as' as:expr]
    ;


except_clause
    =
    'except' ~ [test:test ['as' as:name]]
    ;


suite
    =
      NEWLINE  ~ INDENT stmt+:stmt ~ {EQDENT stmt+:stmt ~}* ~ DEDENT
    | stmpt+:simple_stmt
    ;


namedexpr_test
    =
    name:test [':=' ~ value:test]
    ;


test
    =
    lambdef | ordef
    ;

ordef
    =
    value:or_test [_ 'if' _ condition:or_test _ 'else' ~  _ ~ else:test ]
    ;


test_nocond
    =
    or_test | lambdef_nocond
    ;


lambdef
    =
    'lambda' ~ [args:varargslist] ':' ~ _ exp:test
    ;


lambdef_nocond
    =
    'lambda' [args:varargslist] ':' ~ exp:test_nocond
    ;


or_test
    =
    value:and_test {_ 'or' ~ _ value:and_test}*
    ;


and_test
    =
    value:not_test {_ 'and' ~ _ value:not_test}*
    ;


not_test
    =
    'not' value:not_test | @:comparison
    ;


comparison
    =
    #FIXME: this needs work
    value:expr  op:{_ comp_op _ ~ value:expr}*
    ;


comp_op
    =
    | '=='
    | '>='
    | '<='
    | '<>'
    | '<' !'<'
    | '>' !'>'
    | '!='
    | 'not' 'in'
    | 'in'
    | 'is' 'not'
    | 'is'
    ;


star_expr
    =
    '*' ~ @:expr
    ;


# FIXME: use left/right recursion to fix associativity syntactically
expr
    =
    value:xor_expr {'|' !'=' ~ _ value:xor_expr}*
    ;


xor_expr
    =
    value:and_expr {'^' !'=' ~ _ value:and_expr}*
    ;


and_expr
    =
    value:shift_expr {'&' !'=' ~ _ value:shift_expr}*
    ;


shift_expr
    =
    value:arith_expr {op:('<<' | '>>') !'=' ~ _ value:arith_expr}*
    ;


arith_expr
    =
    value:term {_ op:('+' | '-') !'=' ~ _ op:term}*
    ;


term
    =
    value:factor {_ op:('*' !'*' | '@' | '%' | '//' | '/') !'=' ~ _ value:factor}*
    ;


factor
    =
    op:('+' | '-' | '~') ~ value:factor | value:power
    ;


power
    =
    value:atom_expr ['**' !'=' ~ exp:factor]
    ;


atom_expr
    =
    await:['await' ~] value:atom {trailer}*
    ;


atom
    =
    (
        |
            (
                | '(' _ @:[yield_expr | testlist_comp] _ ')'
                | '[' _ @:[testlist_comp] _ ']'
                | '{' _ @:[dictorsetmaker] _ '}'
                | string
                | number
                | '...'
                | 'None'
                | 'True'
                | 'False'
            ) ~
        | name
    ) ~
    ;


testlist_comp
    =
    (namedexpr_test | star_expr) _
    (
          comp_for
        | {',' _ (namedexpr_test | star_expr) ~ _}* [',']
    )
    ;


trailer
    =
    '(' _ ~ args:[arglist] _ ')' | '[' _ ~ index:subscriptlist _ ']' | _ '.' ~ _ dotname:name
    ;


subscriptlist
    =
    @+:subscript {','  _ @+:subscript _ }* [',']
    ;


subscript
    =
    index:[test] ':' ~ _ ~ to:[test] slice:[sliceop] | index:test
    ;


sliceop
    =
    ':' ~ _ ~ @:[test]
    ;


exprlist
    =
    @+:(expr | star_expr) {',' @+:(expr | star_expr)}* [',']
    ;


testlist
    =
    @+:test {',' @+:test}* [',']
    ;


dictorsetmaker
    =
        (test ':' ~ _ test | '**' expr) _
        (comp_for | {',' _ (test ':' ~ _ test | '**' expr) ~ }* [','])
    |
        (test | star_expr) _
        (comp_for | {','  _ (test | star_expr) ~ }* [','])
    ;


classdef
    =
    'class' ~ name:name ['(' ~ args:[arglist] _ ')'] ':' ~ body:suite
    ;


arglist
    =
    _ @+:argument {',' _ @+:argument}* [',' ~ _]
    ;


argument
    =
    | '**' test
    | '*' test
    | test ':=' ~ _ test
    | test '=' ~ _ test
    | test [ comp_for]
    ;


comp_iter
    =
    _ (comp_for | comp_if)
    ;


sync_comp_for
    =
    'for' ~ vars:exprlist _ 'in' ~ test:_ or_test _ comp:[comp_iter]
    ;


comp_for
    =
    _ async:['async' ~] comp:sync_comp_for
    ;


comp_if
    =
    'if' ~ test:test_nocond comp:[comp_iter]
    ;


encoding_decl
    =
    name
    ;


yield_expr
    =
    'yield' ~
    ('from' ~ from:test | args:testlist_star_expr)
    ;


func_body_suite
    =
      NEWLINE  ~ [type:TYPE_COMMENT ~ NEWLINE ] ~
      INDENT stmt+:stmt ~ {EQDENT stmt+:stmt ~}* DEDENT
    | stmt+:simple_stmt
    ;


func_type_input
    =
    func_type [NEWLINE] ENDMARKER
    ;


func_type
    =
    '(' args:[typelist] ')' '->' ~ ret:test
    ;


typelist
    =
        | '**' ~ test
        | '*' [test] {',' ~ test}* [',' '**' ~ test]
        | ',' ['*' ~ [test] {',' test}* [',' '**' ~ test]]
        | test {',' ~ test}* ['**' ~ test]
    ;


name = NAME ;

@name
NAME = () ;

@name
_old_name
    =
    /[_]*\w[\w_]*/
    ;

number = NUMBER ;

NUMBER = () ;

_old_number
    =
    floatnumber
    | integer
    ;


integer
    =
    bininteger | octinteger | hexinteger | decinteger
    ;


decinteger
    =
    #  nonzerodigit (["_"] digit)* | "0"+ (["_"] "0")*
    /[1-9]([_]?\d)*|0+([_]?0)*/
    ;


bininteger
    =
    # "0" ("b" | "B") (["_"] bindigit)+
    /0[bB]([_]?[01])+/
    ;


octinteger
    =
    # "0" ("o" | "O") (["_"] octdigit)+
    /0[oO]([_]?[0-7])+/
    ;


hexinteger
    =
    # "0" ("x" | "X") (["_"] hexdigit)+
    /0[xX]([_]?[\da-fA-F])+/
    ;


floatnumber
    =
    # floatnumber   ::=  pointfloat | exponentfloat
    # pointfloat    ::=  [digitpart] fraction | digitpart "."
    # exponentfloat ::=  (digitpart | pointfloat) exponent
    # digitpart     ::=  digit (["_"] digit)*
    # fraction      ::=  "." digitpart
    # exponent      ::=  ("e" | "E") ["+" | "-"] digitpart
    | /[.]\d([_]?\d)*([eE][-+]?\d([_]?\d)*)?/
    | /\d([_]?\d)*[.](\d([_]?\d)*)?([eE][-+]?\d([_]?\d)*)?/
    | /\d([_]?\d)*[eE][-+]?\d([_]?\d)*/
    ;


string = {STRING}+ ;

STRING = () ;


__old_string
    =
    (f_string|n_string) ~ {_ (f_string|n_string)}*
    ;

n_string
    =
    STRING
    ;



_OLD_STRING
    =
    &/(r[bu]?|[bu]r?)?('''|""")/ ~
    /(?xs)
    (r[bu]?|[bu]r?)?'''(\\\\|\\'||\\"|\\.|.)*?'''
    |
    (r[bu]?|[bu]r?)?"""(\\\\|\\'||\\"|\\.|.)*?"""
    /
    |
    &/(r[bu]?|[bu]r?)?["']/ ~
    /(?xs)
    (r[bu]?|[bu]r?)?'(\\\\|\\'||\\"|\\.|.)*?'
    |
    (r[bu]?|[bu]r?)?"(\\\\|\\'||\\"|\\.|.)*?"
    /
    ;


f_string
    =
    FSTRING
    ;

FSTRING
    =
    &/f(r[bu]?|[bu]r?)?('''|""")/ ~
    /(?xs)
    f(r[bu]?|[bu]r?)?'''(.|\\|\'|\.)*?'''
    |
    f(r[bu]?|[bu]r?)?"""(.|\\|\"|\.)*?"""
    /
    |
    &/f(r[bu]?|[bu]r?)?["']/ ~
    /(?xs)
    f(r[bu]?|[bu]r?)?'(.|\\|\'|\.)*?'
    |
    f(r[bu]?|[bu]r?)?"(.|\\|\"|\.)*?"
    /
    ;


NEWLINE = () ; # /(?:(?:\s|\\\n)*(?:#[^\n]*?)?\n)+/ ~ | /\s*$/ $;

_ = () ; # /(?:(?:\s|\\\n)*(?:#[^\n]*?)?\n)*/ ;

INDENT = () ; # /(?:\s*(?:#[^\n]*?)?\n)*/  @:/^\s+/ ;

DEDENT = () ; # /(?:\s*(?:#[^\n]*?)?\n)*/  @:&/^\s*/ ;

EQDENT = () ; # /(?:\s*(?:#.[^\n]*?)?\n)*/ @:/^\s*/ ;

ENDMARKER = $ ;

TYPE_COMMENT = () ; # /\s*#\s*type:.*$/ ;

ENCODING = () ;
